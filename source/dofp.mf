% punctuation

% space
begindofchar(oct"040",5,cap#,0,0);
  "Punctutation Space";
endchar;

% period
begindofchar(".",4,xheight#,0,-s#);

  "Punctuation Period";

  pickup currentpen scaled penpuncscale;
  x1=w/2; bot y1=0;
  drawdot z1;
  labels(1);
endchar;

% comma
begindofchar(",",4,xheight#,0,-s#);

  "Punctuation Comma";

  x1=w/2; x2=x1-u;
  bot y1=0; y2=y1-4u;
  draw z1{down}..z2;
  labels(1,2);
endchar;

% colon
begindofchar(":",4,xheight#,0,-s#);

  "Punctuation Colon";

  pickup currentpen scaled penpuncscale;
  x1=x2=w/2; bot y2=0; y1=xheight-y2;
  drawdot z1; drawdot z2;
  labels(1,2);
endchar;

% semicolon
begindofchar(";",4,xheight#,0,-s#);

  "Punctuation Semicolon";

  x1=x2=w/2; x3=x2-u;
  bot y2=0; y1=xheight-y2; y3=y2-4u;
  pickup currentpen scaled penpuncscale;
  drawdot z1; 
  pickup round_pen;
  draw z2{down}..z3;
  labels(range 1 thru 3);
endchar;

% question mark
begindofchar("?",14,cap#,0,-s#);

  "Punctuation Question Mark";

  x1=x2=x4=x6=w/2; lft x5=leftstemloc-.5s; x3=w-x5;
  bot y6=0; bot y1=top y6+u; y2=h/2-u;
  top y4=h+o; y3=y5=.5[y2,y4];
  draw z1--z2;
  super_arc(3,2);
  super_half(3,4,5);
  drawdot z6;
  labels(range 1 thru 6);
  if cutoff:
    z7=bot z1; z8=top z2; z9=bot z5;
    penpos7(penx,0); penpos8(penx,0); 
    penpos5(penx,0); penpos9(penx,0);
    penstroke z7e--z8e; penstroke z5e--z9e;
    penlabels(7,8,5,9);
  fi
endchar;

% exclamation mark
begindofchar("!",8,cap#,0,-s#);

  "Punctuation Exclamation Mark";

  x1=x2=x3=w/2;
  bot y3=0; y1=h-y3; bot y2=top y3+u;
  draw z1--z2; drawdot z3;
  labels(range 1 thru 3);
  if cutoff:
    z4=top z1; z5=bot z2;
    penpos4(penx,0); penpos5(penx,0);
    penstroke z4e--z5e;
    penlabels(4,5);
  fi
endchar;

% double quote (left)
begindofchar(oct"134",10,cap#,0,-2s#);

  "Punctuation Double Quote Left";

  x1=w/2-.7penx; x2=x1; x3=x4=w-x1;
  y3=y1; y2=y4=y1-4u;
  if cutoff:
    y1=h;
    penpos1(penx,0); penpos2(penx,0);
    penpos3(penx,0); penpos4(penx,0);
    penstroke z1e--z2e;
    penstroke z3e--z4e;
    penlabels(range 1 thru 4);
  else:
    top y1=h;
    draw z1--z2; draw z3--z4;
    labels(range 1 thru 4);
  fi 
endchar;

% double quote (right)
begindofchar(oct"042",10,cap#,0,-2s#);

  "Punctuation Double Quote Right";
  
  x1=w/2-.7penx; x2=x1; x3=x4=w-x1;
  y3=y1; y2=y4=y1-4u;
  if cutoff:
    y1=h;
    penpos1(penx,0); penpos2(penx,0);
    penpos3(penx,0); penpos4(penx,0);
    penstroke z1e--z2e;
    penstroke z3e--z4e;
    penlabels(range 1 thru 4);
  else:
    top y1=h;
    draw z1--z2; draw z3--z4;
    labels(range 1 thru 4);
  fi 
endchar;

% single quote (left)
begindofchar(oct"140",4,cap#,0,-2s#);

  "Punctuation Single Quote Left";

  x1=x2=w/2; y2=y1-4u;
  if cutoff:
    y1=h;
    penpos1(penx,0); penpos2(penx,0);
    penstroke z1e--z2e;
  else:
    top y1=h;
    draw z2{up}..z1;
    labels(1,2);
  fi
endchar;

% single quote (right)
begindofchar(oct"047",4,cap#,0,-2s#);

  "Punctuation Single Quote Right";
  
  x1=x2=w/2; y2=y1-4u;
  if cutoff:
    y1=h;
    penpos1(penx,0); penpos2(penx,0);
    penstroke z1e--z2e;
  else:
    top y1=h;
    draw z2{up}..z1;
    labels(1,2);
  fi
endchar;

% (
begindofchar(oct"050",5,cap#,0,-s#);

  "Punctuation Parenthesis Left";

  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  lft x2=leftstemloc-.5s; x1=x3=w-x2;
  bot y1=-u; y3=h-y1; y2=.5[y1,y3];
  super_crescent(1,2,3);
  labels(range 1 thru 3);
endchar;

% )
begindofchar(oct"051",5,cap#,0,-s#);

  "Punctuation Parenthesis Right";
  
  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  rt x2=w-leftstemloc+.5s; x1=x3=w-x2;
  bot y1=-u; y3=h-y1; y2=.5[y1,y3];
  super_crescent(1,2,3);
  labels(range 1 thru 3);
endchar;

% [
begindofchar(oct"133",6,cap#,0,-s#);

  "Punctuation Bracket Left";
  
  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  rt x1=w-leftstemloc+.5s; x4=x1; x2=x3=w-x1;
  bot y1=0-u; y2=y1; y3=y4=h-y1;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

% ]
begindofchar(oct"135",6,cap#,0,-s#);

  "Punctuation Bracket Right";

  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  lft x1=leftstemloc-.5s; x4=x1; x2=x3=w-x1;
  bot y1=0-u; y2=y1; y3=y4=h-y1;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

% {
begindofchar("{",7,cap#,0,-s#);
  
  "Punctuation Curly Bracket Left";

  lft x2=leftstemloc-.5s-o; x1=x3=w-x2-o;
  x4=x5=.5[x1,x2];
  bot y1=0-u; y2=h/2; y3=h-y1;
  y4=.5[y1,y2]; y5=.5[y2,y3];
  super_arc(4,1); super_arc(4,2);
  super_arc(5,2); super_arc(5,3);
  labels(range 1 thru 5); 
endchar;

% }
begindofchar("}",7,cap#,0,-s#);

  "Punctuation Curly Bracket Right";

  rt x2=w-leftstemloc+.5s+o; x1=x3=w-x2-o;
  x4=x5=.5[x1,x2];
  bot y1=0-u; y2=h/2; y3=h-y1;
  y4=.5[y1,y2]; y5=.5[y2,y3];
  super_arc(4,1); super_arc(4,2);
  super_arc(5,2); super_arc(5,3);
  labels(range 1 thru 5); 
endchar;

% «
begindofchar(oct"253",14,xheight#,0,-s#);
  
  "Punctuation Guillemets Left";

  lft x2=leftstemloc; x1=x3=x2+3u;
  x4=x6=w-x2; x5=x4-3u;
  top y1=h-u; y4=y1;
  bot y3=u; y6=y3;
  y2=y5=h/2;
  if cutoff:
    penpos1(penx,0); penpos2(compactcor*penx,0); 
    penpos3(penx,0); penpos4(penx,0); 
    penpos5(compactcor*penx,0); penpos6(penx,0);
    penstroke z1e--z2e--z3e;
    penstroke z4e--z5e--z6e;
    penlabels(1,2,3,4,5,6);
  else:
    draw z1--z2--z3;
    draw z4--z5--z6;
    labels(1,2,3,4,5,6);
  fi
endchar;

% «
begindofchar(oct"273",14,xheight#,0,-s#);
  
  "Punctuation Guillemets Right";

  rt x2=w-leftstemloc; x1=x3=x2-3u;
  x4=x6=w-x2; x5=x4+3u;
  top y1=h-u; y4=y1;
  bot y3=u; y6=y3;
  y2=y5=h/2;
  if cutoff:
    penpos1(penx,0); penpos2(compactcor*penx,0); 
    penpos3(penx,0); penpos4(penx,0); 
    penpos5(compactcor*penx,0); penpos6(penx,0);
    penstroke z1e--z2e--z3e;
    penstroke z4e--z5e--z6e;
    penlabels(1,2,3,4,5,6);
  else:
    draw z1--z2--z3;
    draw z4--z5--z6;
    labels(1,2,3,4,5,6);
  fi
endchar;

% /
begindofchar(oct"057",12,cap#,0,-s#);

  "Punctuation Slash";

  lft x1=leftstemloc-.5s; x2=w-x1;
  y2=h-y1;
  if cutoff:
    y1=0; 
    penpos1(penx,0); penpos2(penx,0); 
    penstroke z1e--z2e;
    penlabels(1,2);
  else:
    bot y1=0; 
    draw z1--z2;
    labels(1,2);
  fi
endchar;

% \
begindofchar("\",12,cap#,0,-s#);

  "Punctuation Backslash";

  rt x1=w-leftstemloc+.5s; x2=w-x1;
  y2=h-y1;
  if cutoff:
    y1=0; 
    penpos1(penx,0); penpos2(penx,0); 
    penstroke z1e--z2e;
    penlabels(1,2);
  else:
    bot y1=0; 
    draw z1--z2;
    labels(1,2);
  fi
endchar;

% |
begindofchar("|",8,cap#,0,-s#);

  "Punctuation Vertical Bar";

  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  x1=x2=w/2;
  bot y1=0; y2=h-y1;
  draw z1--z2;
  labels(1,2);
endchar;

% _
begindofchar("_",18,xheight#,0,-2s#);

  "Punctuation Underscore";

  if cutoff: pickup pensquare xscaled penx yscaled peny; fi
  lft x1=0; x2=w-x1;
  top y1=0; y2=y1;
  draw z1--z2;
  labels(range 1 thru 2);
endchar;
