% symbols

% &
begindofchar("&",14,cap#,0,0);

  "Symbol Ampersand";

  rt x1=w-.25s; lft x6=leftstemloc; x8=w-x6+.5u; 
  x2=x6+u; x4=w-x2; x3=x5=x7=w/2;
  bot y1=0; y7=y1-o; y3=h-y7;
  y5=h/2+.4u; y6=.5[y7,y5]; y8=.3[y7,y6]+peny;
  y2=y4=.5[y5,y3]; 
  super_half(2,3,4);
  super_half(6,7,8);
  draw z4{down}..z6{down};
  labels(range 1 thru 8);
  if cutoff:
    z9=top z8; z10=bot z1;
    penpos2(penx,0); penpos8(penx,0);
    penpos9(penx,0); penpos10(penx,0);
    penstroke z8e--z9e; penstroke z2e{down}..tension 2..z10e;
    penlabels(2,8,9,10);
  else:
    draw z2{down}..tension 2..z1;
  fi
endchar;

% @
begindofchar(oct"100",20,cap#,0,0);

  "Symbol at";

  lft x5=leftstemloc-o; x3=w-x5;
  x4=x6=x7=x8=x10=w/2;
  x9=.6[x5,x4]; x1=.6[x3,x4]; x2=x1;
  bot y6=0-o; y4=xheight+3u+o;
  y3=y5=y9=.5[y6,y4]; y7=y6+overdraw;
  y8=.3[y4,y9]; y10=.3[y6,y9];
  y1=.6[y8,y9]; y2=.6[y10,y9];
  pickup currentpen scaled .8;
  draw z1--z2;
  super_half(9,8,1);
  super_half(9,10,2);
  pickup round_pen;
  draw rt z2{down}..z3{up};
  super_half(3,4,5);
  super_arc(5,6);
  labels(range 1 thru 10);
  if cutoff:
    z11=top z6; penpos6(penx,0); penpos11(penx,0);
    penstroke z6e--z11e;
    penlabels(6,11);
  fi
  % phew
endchar;

% $
begindofchar(oct"044",14,cap#,0,0);

  "Symbol Dollar";

  lft x1=leftstemloc; x5+o=x1;
  x3=w-x1; x7=x3; x2=x4=x6=x8=x9=w/2;
  y2+o=.4[y8,y3]; y6=h-y2; y4=h/2;
  y3=.5[y4,y2]; y5=.5[y6,y4];
  y1=.3[y2,y4]; y7=.3[y6,y4];
  bot y8=0; y9=h-y8;
  super_half(1,2,3);
  super_half(5,6,7);
  draw z3{up}..z5{up};
  labels(range 1 thru 9);
  if cutoff:
    z10=top z1; z11=bot z7;
    z12=bot z8; z13=top z9;
    penpos1(penx,0); penpos10(penx,0);
    penpos7(penx,0); penpos11(penx,0);
    penpos12(compactcor*penx,0); penpos13(compactcor*penx,0);
    penstroke z1e--z10e;
    penstroke z7e--z11e;
    penstroke z12e--z13e;
    penlabels(1,7,8,9,10);
  else:
    draw z8--z9;
  fi
endchar;

% +
begindofchar(oct"053",18,xheight#,0,-s#);

  "Symbol Plus";

  x1=x2=w/2; bot y1=0; 
  lft x3=leftstemloc-.5s; x4=w-x3;
  y2=h-y1; y3=y4=h/2;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
  if cutoff:
    z5=bot z1; z6=top z2;
    z7=lft z3; z8=rt z4;
    penpos5(penx,0); penpos6(penx,0);
    penpos7(peny,90); penpos8(peny,90);
    penstroke z5e--z6e; penstroke z7e--z8e;
    penlabels(5,6,7,8);
  fi
endchar;

% -
begindofchar(oct"055",18,xheight#,0,-s#);

  "Symbol Minus";

  if cutoff:
    x1=leftstemloc-.5s; x2=w-x1;
  else:
    lft x1=leftstemloc-.5s; x2=w-x1;
  fi
  y1=y2=h/2;
  if cutoff:
    penpos1(peny,90); penpos2(peny,90);
    penstroke z1e--z2e;
    penlabels(1,2);
  else:
    draw z1--z2;
    labels(range 1 thru 2);
  fi
endchar;

% =
begindofchar(oct"075",18,xheight#,0,-s#);

  "Symbol Equal Sign";

  if cutoff:
    x1=leftstemloc-.5s; x3=x1; x2=x4=w-x1;
  else:
    lft x1=leftstemloc-.5s; x3=x1; x2=x4=w-x1;
  fi
  y1=y2=h/3; y3=y4=h-y1; 
  if cutoff:
    penpos1(peny,90); penpos2(peny,90);
    penpos3(peny,90); penpos4(peny,90);
    penstroke z1e--z2e;
    penstroke z3e--z4e;
    penlabels(1,2,3,4);
  else:
    draw z1--z2; draw z3--z4;
    labels(range 1 thru 4); 
  fi
endchar;

% <
begindofchar(oct "074",18,xheight#,0,-s#);

  "Symbol Smaller Sign";

  lft x2=leftstemloc-.5s; x1=x3=w-x2;
  bot y1=0; y3=h-y1; y2=h/2;
  if cutoff:
    penpos1(1.3peny,90); penpos2(1.3peny,90); penpos3(1.3peny,90);
    penstroke z1e--z2e; penstroke z2e--z3e;
    penlabels(1,2,3);
  else:
    draw z1--z2--z3;
    labels(range 1 thru 3);
  fi
endchar;

% >
begindofchar(oct"076",18,xheight#,0,-s#);

  "Symbol Bigger Sign";
  
  rt x2=w-leftstemloc+.5s; x1=x3=w-x2;
  bot y1=0; y3=h-y1; y2=h/2;
  if cutoff:
    penpos1(1.3peny,90); penpos2(1.3peny,90); penpos3(1.3peny,90);
    penstroke z1e--z2e; penstroke z2e--z3e;
    penlabels(1,2,3);
  else:
    draw z1--z2--z3;
    labels(range 1 thru 3);
  fi
endchar;

% *
begindofchar(oct"052",14,cap#,0,-s#);

  "Symbol Asteriks";
  
  x1=x2=w/2; y1=y2-(h-xheight+u);
  if cutoff:
    y2=h; 
  else:
    top y2=h; 
  fi
  z3=z1 rotatedaround (.5[z1,z2],60);
  z4=z2 rotatedaround (.5[z1,z2],60);
  z5=z3 rotatedaround (.5[z1,z2],60);
  z6=z4 rotatedaround (.5[z1,z2],60);
  if cutoff:
    penpos1(peny,0); penpos2(peny,0);
    penpos3(peny,45); penpos4(peny,45);
    penpos5(peny,-45); penpos6(peny,-45);
    penstroke z1e--z2e; penstroke z3e--z4e; penstroke z5e--z6e;
    penlabels(range 1 thru 6);
  else:
    draw z1--z2; draw z3--z4; draw z5--z6;
    labels(range 1 thru 6);
  fi
endchar;

% %
begindofchar(oct"045",20,cap#,0,-s#);

  "Symbol Percentage Sign";
  
  lft x1=leftstemloc-.5s; x2=x4=w-x1;
  x6=.25[w/2,w]; x3=x5=.5[x6,x4];
  if cutoff:
    y1=0;
  else:
    bot y1=0; 
  fi
  y2=h-y1; y3=y1-o; y5=.75[0,w/2];
  y4=y6=.5[y3,y5]-y3;
  z7=z3 rotatedaround ((w/2,h/2),180);
  z8=z4 rotatedaround ((w/2,h/2),180);
  z9=z5 rotatedaround ((w/2,h/2),180);
  z10=z6 rotatedaround ((w/2,h/2),180);
  if opendraw:
    super_crescent(5,6,3); super_arc(4,3);
    super_crescent(7,8,9); super_arc(10,9);
  else:
    super_crescent(3,4,5); super_crescent(5,6,3);
    super_crescent(7,8,9); super_crescent(9,10,7);
  fi
  labels(range 1 thru 10);
  if cutoff:
    penpos1(penx,0); penpos2(penx,0);
    penstroke z1e--z2e;
    penlabels(1,2);
  else:
    draw z1--z2;
  fi
endchar;

% #
begindofchar(oct"043",18,xheight#,0,-s#);

  "Symbol Hash Sign";
  
  lft x1=leftstemloc-.5s; x2+2u=w-x1; x3=w-x2; x4=w-x1;
  y1=y2=xheight-barheight; y3=y4=barheight;
  bot y5=0; y7=y5; y6=y8=h-y5;
  z5=whatever[z1,z3]+(.2[x1,x2]-x1,0); x8=w-x5;
  z7=whatever[z2,z4]-(.2[x1,x2]-x1,0); x6=w-x7;
  labels(range 1 thru 8);
  if cutoff:
    z9=lft z1; z10=rt z2;
    z11=lft z3; z12=rt z4;
    y13=0; z13=whatever[z5,z6];
    y14=h; z14=whatever[z5,z6];
    y15=0; z15=whatever[z7,z8];
    y16=h; z16=whatever[z7,z8];
    penpos9(peny,90); penpos10(peny,90);
    penpos11(peny,90); penpos12(peny,90);
    penpos13(penx,0); penpos14(penx,0);
    penpos15(penx,0); penpos16(penx,0);
    penstroke z9e--z10e; penstroke z11e--z12e;
    penstroke z13e--z14e; penstroke z15e--z16e;
    penlabels(range 9 thru 16);
  else:
    draw z1--z2; draw z3--z4;
    draw z5--z6; draw z7--z8;
  fi
endchar;
