% special upper case

% ligature OE
begindofchar(oct"036",20,cap#,0,-.5s#);
  % part "O"
  x1=w/2; x2=.5[x1,x3]; 
  lft x3=leftstemloc-o-.25s;
  x4=x2; x5=x1;
  y1=.6[y3,y2]; top y2=h+o;
  y3=h/2; y4=h-y2; y5=h-y1;
  labels(range 1 thru 5);
  super_half(1,2,3);
  super_half(3,4,5);
  % part "E"
  x6=x7=x9=x1; x8=x10+u=x11=w-x3-o;
  y6=y4+o; y7=h-y6; y8=y7;
  y9=y10=y3; y11=y6;
  labels(range 6 thru 11);
  draw z6--z7; draw z7--z8;
  draw z9--z10; draw z6--z11;
  if cutoff:
    z12=bot z6; z13=top z7;
    z14=rt z8; z15=rt z10;
    z16=rt z11;
    penpos6(penx,0); penpos12(penx,0);
    penpos7(penx,0); penpos13(penx,0);
    penpos8(peny,90); penpos14(peny,90);
    penpos10(peny,90); penpos15(peny,90);
    penpos11(peny,90); penpos16(peny,90);
    penlabels(6,7,8,10,11,12,13,14,15,16);
    penstroke z6e--z12e; penstroke z7e--z13e;
    penstroke z8e--z14e; penstroke z10e--z15e;
    penstroke z11e--z16e;
  fi
endchar;

% ligature AE
begindofchar(hex"C6",20,cap#,0,-.5s#);
  if cutoff:
    lft x1=leftstemloc-.25s; x2=x3=x4-.35penx;
    x4=x5=x7=x9=x11=w/2+u; 
    x8=w-leftstemloc+.25s; x10+u=x12=x8;
    y1=y5=0; y2=h; top y3=h; y4=y2; y8=y3;
    top y6=barheight-u; y7=y6;
    z6=whatever[z1,z2];
    y9=y10=h/2; bot y11=0; y12=y11;
    penpos1(penx,0); penpos2(compactcor*penx,0); 
    penpos3(peny,90); penpos4(penx,0);
    penpos5(penx,0); penpos6(peny,90);
    penpos7(peny,90); penpos8(peny,90);
    penpos9(peny,90); penpos10(peny,90);
    penpos11(peny,90); penpos12(peny,90);
    penlabels(range 1 thru 12);
    penstroke z1e--z2e; penstroke z3e--z8e;
    penstroke z4e--z5e; penstroke z6e--z7e;
    penstroke z9e--z10e; penstroke z11e--z12e;
  else:
    lft x1=leftstemloc-.25s; x2=x4-.35penx; 
    x4=x5=x7=x8=w/2; x3=x9+u=x10=w-x1;
    bot y1=0; y2=y3=y4=h-y1; y5=y10=y1; 
    top y6=barheight-u; y7=y6; y8=y9=h/2;
    z6=whatever[z1,z2];
    labels(range 1 thru 10);
    draw z1--z2--z3; draw z4--z5;
    draw z6--z7; draw z8--z9;
    draw z5--z10;
  fi
endchar;
