% digits

begindofchar("0",14,figheight#,0,0);

  "Digit 0";

  x1=x3=w/2; lft x4=leftstemloc-o; x2=w-x4;
  bot y1=0-o; top y3=h+o; y2=y4=h/2;
  x5=x3+u; top y5=bot y3-u;
  if opendraw:
    super_crescent(3,4,1);
    super_crescent(1,2,5);
    labels(range 1 thru 5);
  else:
    super_half(4,1,2);
    super_half(4,3,2);
    labels(range 1 thru 4);
  fi
endchar;

begindofchar("1",10,figheight#,0,-.75s#);

  "Digit 1";

  lft x1=leftstemloc-.7s; rt x2=w-leftstemloc; 
  x3=x4+overdraw=x5-overdraw=x2;
  top y2=h; bot y3=0; y4=y5=y3;
  y1=h-(1[x1,x2]-x1);
  labels(range 1 thru 5);
  if cutoff:
    z6=bot z3; z7=top z2; z8=z1;
    penpos6(penx,0); penpos7(penx,0); penpos8(penx,0);
    penstroke z6e--z7e; penstroke z7e--z8e;
    penlabels(6,7,8);
  else:
    draw z1--z2--z3; draw z4--z5;
  fi
endchar;

begindofchar("2",14,figheight#,0,0);

  "Digit 2";

  lft x1=leftstemloc; x4=x1; 
  x3=x5=w-x1; x2=.5[x1,x3];
  bot y4=0; y5=y4; top y2=h+o;
  y1=.3[y2,h/2]; y3=.5[y2,h/2];
  super_half(1,2,3);
  draw z3{down}..z4{down};
  draw z4--z5;
  labels(range 1 thru 5);
  if cutoff:
    z6=bot z1; z7=lft z4; z8=rt z5;
    penpos1(penx,0); penpos6(penx,0);
    penpos7(peny,90); penpos8(peny,90);
    penstroke z1e--z6e; penstroke z7e--z8e;
    penlabels(1,6,7,8);
  fi
endchar;

begindofchar("3",14,figheight#,0,0);

  "Digit 3";

  lft x1=leftstemloc; x7=x1;
  rt x3=w-leftstemloc+o; x5=x3;
  x2=x4=x6=w/2;
  bot y2=0-o; y6=h-y2; bot y4=barheight;
  y3=.5[y2,y4]; y5=.5[y4,y6];
  y1=.3[y2,y3]; y7=h-y1;
  super_crescent(2,3,4);
  super_crescent(4,5,6);
  super_arc(1,2);
  super_arc(7,6);
  labels(range 1 thru 7);
  if cutoff:
    z8=top z1; z9=lft z4; z10=bot z7;
    penpos1(penx,0); penpos8(penx,0);
    penpos4(peny,90); penpos9(peny,90);
    penpos7(penx,0); penpos10(penx,0);
    penstroke z1e--z8e;
    penstroke z4e--z9e;
    penstroke z7e--z10e;
    penlabels(1,4,7,8,9,10)
  fi
endchar;

begindofchar("4",14,figheight#,0,-.75s#);

  "Digit 4";

  x1=x2=w/2+u; lft x4=leftstemloc-.25s; 
  rt x5=w-leftstemloc+.5s; x3=.3[x4,x5];
  bot y1=0; top y2=xheight; 
  y3=h-y1; bot y4=h-xheight; y5=y4;
  labels(range 1 thru 5);
  if cutoff:
    z6=bot z1; z7=top z2;
    y8=top y3; z8=whatever[z3,z4];
    y9=bot y4; z9=whatever[z3,z4];
    z10=rt z5;
    penpos6(penx,0); penpos7(penx,0);
    penpos8(compactcor*penx,0); penpos9(compactcor*penx,0);
    penpos4(peny,90); penpos10(peny,90);
    penstroke z6e--z7e;
    penstroke z8e--z9e;
    penstroke z4e--z10e;
    penlabels(4,6,7,8,9,10);
  else:
    draw z1--z2; draw z3--z4--z5;
  fi
endchar;

begindofchar("5",14,figheight#,0,0);

  "Digit 5";

  lft x1=leftstemloc; x5=x6=x1;
  x3-o=x7=w-x1; x2=x4=w/2;
  bot y2=0-o; y4=xheight-y2;
  y3=xheight/2; top y6=h; y7=y6;
  y1=.3[y2,y3]; y5=xheight-y1;
  super_half(1,2,3);
  super_half(3,4,5);
  draw z5--z6--z7;
  labels(range 1 thru 7);
  if cutoff:
    z8=top z1; z8l=lft z8; z8r=rt z8+expand*right; penpos1(penx,0);
    z9=bot z5; z9l=lft z9; z9r=rt z9+expand*right; penpos5(penx,0);
    z10=lft z6; z11=rt z7; penpos10(peny,90); penpos11(peny,90);
    penstroke z1e--z8e; penstroke z5e--z9e; penstroke z10e--z11e;
    penlabels(1,5,8,9,10,11);
  fi
endchar;

begindofchar("6",14,figheight#,0,0);

  "Digit 6";

  x1=x3=x5+u=w/2; lft x4=leftstemloc-o; x2=x6+u=w-x4;
  bot y1=0-o; y3=xheight-y1; 
  y5=h-y1; y2=y4=xheight/2;
  y6=.45[y5,y3];
  super_half(4,1,2);
  super_half(4,3,2);
  super_half(4,5,6);
  labels(range 1 thru 6);
  if cutoff:
    z7=bot z6; 
    z7l=lft z7+expand*left; z7r=rt z7; penpos6(penx,0);
    penstroke z6e--z7e;
    penlabels(6,7);
  fi
endchar;

begindofchar("7",14,figheight#,0,-s#);

  "Digit 7";

  lft x3=leftstemloc-.5s; x2=w-x3; x1=.3[x3,x2];
  bot y1=0; y2=y3=h-y1;
  labels(range 1 thru 3);
  if cutoff:
    y4=0; z4=whatever[z1,z2];
    y5=bot y2; z5=whatever[z1,z2];
    x6=x5; y6=h; 
    z7=lft z3;
    penpos4(penx,0); penpos5(penx,0); penpos6(penx,0); 
    penpos2(peny,90); penpos7(peny,90);
    penstroke z4e--z5e--z6e;
    penstroke z2e--z7e;
    penlabels(2,4,5,6,7);
  else:
    draw z1--z2--z3;
  fi
endchar;

begindofchar("8",14,figheight#,0,0);

  "Digit 8";

  x1=x3=x5=w/2; lft x7=leftstemloc-o; 
  x4=x7+.2u; x2=w-x7; x6=w-x4;
  bot y1=0-o; y5=h-y1; y3=h/2+.4u;
  y2=y7=.5[y1,y3]; y4=y6=.5[y3,y5];
  super_half(7,1,2);
  super_half(7,3,2);
  super_half(4,3,6);
  super_half(4,5,6);
  labels(range 1 thru 7);
endchar;

begindofchar("9",14,figheight#,0,0);

  "Digit 9";

  x1=x3=x5-u=w/2; lft x2=leftstemloc-o; 
  x4=w-x2; x6=x2+u;
  bot y5=0-o; y1=h-y5;
  bot y3+o=h-xheight;
  y2=y4=.5[y3,y1]; y6=.45[y5,y3];
  super_half(2,1,4);
  super_half(2,3,4);
  super_half(4,5,6);
  labels(range 1 thru 6);
  if cutoff:
    z7=top z6;
    penpos6(penx,0); z7l=lft z7; z7r=rt z7+expand*right;
    penstroke z6e--z7e;
    penlabels(6,7);
  fi
endchar;