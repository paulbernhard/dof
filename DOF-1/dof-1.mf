mode_setup;

% parameters

% u# -> basic character unit
% cap# -> height of characters (no higher ascenders)
% desc# -> depth of descenders
% xheight# -> height of lowercase characters (without descenders)
% s# -> sidebars left/right of character
% o# -> vertical/horizontal overshoot for rounded character edges
% xgap# -> shortening for x-strokes, as in F
% ygap# -> vertical gap for stroke endings, as in C or e
% barheight# -> height of H-bar relative to cap#
% ebarheight# -> height of e-bar relative to xheight#

% penx# -> pen width
% peny# -> pen height
% penangle -> angle of pen

% slant:=0; -> slanting for italicizing typefaces
% overdraw# -> serifizing by "over"-drawing characters edges

slant:=0;
u#:=3/10pt#; cap#:=9pt#; desc#:=3pt#; xheight#:=.6cap#;
s#:=2u#; o#:=1/18pt#;
xgap#:=u#; ygap#:=3u#; barheight#:=.5*cap#; ebarheight#:=.5xheight#; overdraw#:=1/2u#;
leftstemloc#:=s#;
penx#:=0.8pt#; peny#:=0.55pt#; penangle:=0;

% define pixels / pen(s)
define_pixels(u,cap,desc,xheight,s,xgap,ygap,overdraw);
define_corrected_pixels(o);
define_blacker_pixels(penx,peny);
pickup pencircle xscaled penx yscaled peny rotated penangle; dof_pen:=savepen;
define_good_y_pixels(barheight,ebarheight);
define_good_x_pixels(leftstemloc);

def begindofchar(expr code,width_in_units,height,desc) = 
  beginchar(code,width_in_units*u#+2*s#,height,desc);
  pickup dof_pen; enddef;

% change currenttransform if slant is > 0;
if unknown slant: slant:=0 else: currenttransform:=
  identity slanted slant fi;

begindofchar("A",16,cap#,0);
  lft x1=leftstemloc; x4=w-x1;
  bot y1=bot y4=0;
  x2+1.5xgap=x3-1.5xgap=w/2;
  top y2=top y3=h;
  y5=y6=barheight;
  z5=whatever[z1,z2];
  z6=whatever[z3,z4];
  draw z1--z2--z3--z4;
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("B",16,cap#,0);
  lft x1=lft x2=leftstemloc; bot y1=top y2-h=0;
  lft x3=lft x5=lft x7=leftstemloc-overdraw; y3=y2; bot y5=barheight; y7=y1;
  x4=w-x1; x6=w-x1+o;
  y4=y5+(y3-y5)/2; y6=y7+(y5-y1)/2;
  draw z1--z2;
  draw z3{right}..z4{down}..z5{left};
  draw z5{right}..z6{down}..z7{left};
  labels(range 1 thru 7);
endchar;

begindofchar("C",16,cap#,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=w-x3;
  y1=ygap; y5=h-y1;
  x2=x4=w/2;
  bot y2+o=top y4-h-o=0;
  draw z1..z2{left}..z3{up}..z4{right}..z5;
  labels(range 1 thru 5);
endchar;

begindofchar("D",16,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw;
  bot y1=top y2-h=0; y3=y2; y5=y1;
  x4=w-x1+o; y4=h/2;
  draw z1--z2;
  draw z3{right}..z4{down}..z5{left};
  labels(range 1 thru 5);
endchar;

begindofchar("E",14,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x5=x7=x1-overdraw;
  bot y1=top y2-h=0; y3=y2; y7=y1; bot y5=barheight;
  x4=x8=w-x1; x6=w-x1-xgap;
  y4=y3; y6=y5; y8=y7;
  draw z1--z2;
  draw z3--z4; draw z5--z6; draw z7--z8;
  labels(range 1 thru 8);
endchar;

begindofchar("F",14,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw;
  bot y1=top y2-h=0; y3=y2; bot y5=barheight;
  x4=w-x1; x6=w-x1-xgap;
  y4=y3; y6=y5;
  draw z1--z2;
  draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("G",16,cap#,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=x6=x7-overdraw=w-x3;
  y5-ygap=0; y1=h-y5; bot y6=barheight; y6=y7=y8;
  x2=x4=x8=w/2; top y2-h-o=bot y4+o=0;
  draw z1..z2{left}..z3{down}..z4{right}..z5;
  draw z5--z6; 
  draw z7--z8;
  labels(range 1 thru 8);
endchar;

begindofchar("H",16,cap#,0);
  lft x1=leftstemloc; x1=x2=x5+overdraw; x3=x4=x6-overdraw=w-x1;
  bot y1=bot y3=top y2-h=0; y2=y4; bot y5=bot y6=barheight;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("I",5,cap#,0);
  x1=x2=w/2; x3=x5=x1-overdraw; x4=x6=x1+overdraw;
  bot y1=top y2-h=0; y1=y3=y4; y2=y5=y6;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("J",10,cap#,0);
  lft x4=leftstemloc; x1=x2=w-x4; x3=w/2; 
  x5+overdraw=x6-overdraw=x1;
  bot y3+o=top y1-h=0; y2=y4=x2-x3; y5=y6=y1;
  draw z1..z2{down}..z3{left}..z4{up};
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("K",16,cap#,0);
  lft x1=lft x2=leftstemloc; 
  x3=x1; rt x1=lft x5;
  x4=x6=w-x1;
  bot y1=top y2-h=0; y1=y6; y2=y4; 
  y3=y5=barheight;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("L",14,cap#,0);
  lft x1=leftstemloc; x1=x2; x3=x1-overdraw; x4=w-x1;
  bot y1=top y2-h=0; y1=y3=y4;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("M",20,cap#,0);
  lft x1=lft x2=leftstemloc; x4=x5=w-x1; x3=w/2;
  bot y1=top y2-h=0; y1=y5; y2=y4; y3=ygap;
  draw z1--z2--z3--z4--z5;
  labels(range 1 thru 5);
endchar;

begindofchar("N",16,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x4=w-x1;
  bot y1=top y2-h=0; y1=y3; y2=y4;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("O",20,cap#,0);
  x1=x3=w/2; lft x4+o=leftstemloc; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  draw z1..z2..z3..z4..cycle;
  labels(range 1 thru 4);
endchar;

begindofchar("P",16,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw; x4=w-x1+o;
  bot y1=top y2-h=0; y3=y2; bot y5=barheight; y4=y5+(y3-y5)/2;
  draw z1--z2; draw z3{right}..z4{down}..z5{left};
  labels(range 1 thru 5);
endchar;

begindofchar("Q",20,cap#,0);
  x1=x3=w/2; lft x4+o=leftstemloc; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  bot rt z6=(w,0); z5=.45[z6,(w/2,h/2)];
  draw z1..z2..z3..z4..cycle;
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("R",16,cap#,0);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw;
  x4=x7=w-x1+o; lft x6=rt x1;
  bot y1=top y2-h=0; y3=y2; y7=y1; bot y5=barheight; y6=y5;
  y4=y5+(y3-y5)/2;
  draw z1--z2; draw z3{right}..z4{down}..z5{left}; draw z6--z7;
  labels(range 1 thru 7);
endchar;

font_identifier:="DOF-1";
% font_coding_scheme:="";
font_size 10pt#;

end