% dof-1 uppercase characters

begindofchar("A",16,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1;
  x2+1.5xgap=x3-1.5xgap=w/2;
  bot y1=0; y4=y1; top y2=h; y3=y2;
  y5=y6=barheight;
  z5=whatever[z1,z2];
  z6=whatever[z3,z4];
  draw z1--z2--z3--z4;
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("B",14,cap#,0,0);
  lft x1=leftstemloc; x2=x1; 
  x3=x6=x8=x1-overdraw; x5=x1+2xgap;
  bot y1=top y2-h=0; y8=y1; y3=y2; y5=y6=barheight;
  x4=w-x1; x7=w-x1+o;
  y4=y6+(y3-y6)/2; y7=y8+(y6-y1)/2;
  draw z1--z2;
  draw z3{right}..z4{down}..z5{left}--z6;
  draw z6--z5{right}..z7{down}..z8{left};
  labels(range 1 thru 7);
endchar;

begindofchar("C",14,cap#,0,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=w-x3+.25s;
  y1=ygap; y5=h-y1;
  x2=x4=w/2;
  bot y2+o=top y4-h-o=0;
  draw z1..z2{left}..z3{up}..z4{right}..z5;
  labels(range 1 thru 5);
endchar;

begindofchar("D",14,cap#,0,-.25s#);
  lft x1=leftstemloc; x2=x3+overdraw=x5+overdraw=x1;
  rt x4=w-leftstemloc+o+.25s;
  bot y1=0; y5=y1; top y2=h; y3=y2; y4=h/2;
  draw z1--z2; draw z3{right}..z4{down}..z5{left};
  labels(range 1 thru 5);
endchar;

begindofchar("E",14,cap#,0,-.25s#);
  lft x1=lft x2=leftstemloc; x3=x5=x7=x1-overdraw;
  x4=w-x1+.25s; x6+xgap=x8=x4;
  bot y1=top y2-h=0; y3=y2; y7=y1; bot y5=barheight;
  y4=y3; y6=y5; y8=y7;
  draw z1--z2;
  draw z3--z4; draw z5--z6; draw z7--z8;
  labels(range 1 thru 8);
endchar;

begindofchar("F",14,cap#,0,-.25s#);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw;
  x4=w-x1+.25s; x6+xgap=x4;
  bot y1=top y2-h=0; y3=y2; 
  bot y5=barheight; y4=y3; y6=y5;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("G",14,cap#,0,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=x6=x7-overdraw=w-x3;
  y5-ygap=0; y1=h-y5; y6=barheight; y6=y7=y8;
  x2=x4=x8=w/2; top y2-h-o=bot y4+o=0;
  draw z1..z2{left}..z3{down}..z4{right}..z5;
  draw z5--z6; 
  draw z7--z8;
  labels(range 1 thru 8);
endchar;

begindofchar("H",14,cap#,0,0);
  lft x1=leftstemloc; x1=x2=x5+overdraw; x3=x4=x6-overdraw=w-x1;
  bot y1=bot y3=top y2-h=0; y2=y4; bot y5=bot y6=barheight;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("I",7,cap#,0,0);
  x1=x2=w/2; x3=x5=x1-overdraw; x4=x6=x1+overdraw;
  bot y1=top y2-h=0; y1=y3=y4; y2=y5=y6;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("J",12,cap#,0,-.5s#);
  rt x1=w-leftstemloc; x2=x5+overdraw=x6-overdraw=x1;
  lft x4=leftstemloc-.5s; x3=.5[x4,x2];
  top y1=h; y5=y6=y1; 
  bot y3=0; y2=y4=.5[x4,x2]-x4; 
  draw z1..z2{down}..z3{left}..z4{up}; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("K",14,cap#,0,-.25s#);
  lft x1=leftstemloc; x2=x3=x1; rt x1=lft x5;
  rt x4=w-leftstemloc+.25s; x6=x4;
  bot y1=0; top y2=h; y1=y6; y2=y4; 
  y3=y5=barheight;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("L",14,cap#,0,-.5s#);
  lft x1=leftstemloc; x1=x2; 
  x3+overdraw=x1; rt x4=w-leftstemloc+.5s;
  bot y1=top y2-h=0; y1=y3=y4;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("M",20,cap#,0,0);
  lft x1=lft x2=leftstemloc; x4=x5=w-x1; x3=w/2;
  bot y1=top y2-h=0; y1=y5; y2=y4; y3=ygap;
  draw z1--z2--z3--z4--z5;
  labels(range 1 thru 5);
endchar;

begindofchar("N",14,cap#,0,0);
  lft x1=lft x2=leftstemloc; x3=x4=w-x1;
  bot y1=top y2-h=0; y1=y3; y2=y4;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("O",20,cap#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  draw z1..z2..z3..z4..cycle;
  labels(range 1 thru 4);
endchar;

begindofchar("P",14,cap#,0,-.25s#);
  lft x1=leftstemloc; x2=x1; 
  x3+overdraw=x5+overdraw=x1; rt x4=w-leftstemloc+o+.25s;
  bot y1=0; top y2=h; y3=y2; y5=barheight; y4=.5[y3,y5];
  draw z1--z2; draw z3{right}..z4{down}..z5{left};
  labels(range 1 thru 5);
endchar;

begindofchar("Q",20,cap#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  bot rt z6=(w-leftstemloc,0); z5=.45[z6,(w/2,h/2)];
  draw z1..z2..z3..z4..cycle;
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("R",14,cap#,0,0);
  lft x1=leftstemloc; x2=x3+overdraw=x5+overdraw=x1;
  lft x6=rt x1; rt x4=w-leftstemloc+o+.5u; x7=x4;
  bot y1=0; y7=y1; top y2=h; y3=y2; 
  y5=barheight; y6=y5; y4=.5[y3,y5];
  draw z1--z2; draw z3{right}..z4{down}..z5{left}; draw z6--z7;
  labels(range 1 thru 7);
endchar;

begindofchar("S",14,cap#,0,0);
  lft x1=leftstemloc; x5+o=x1;
  x3=w-x1; x7=x3; x2=x4=x6=w/2;
  bot y2+o=0; top y6-o=h; y4=h/2;
  y3=.5[y4,y2]; y5=.5[y6,y4];
  y1=.3[y2,y4]; y7=.3[y6,y4];
  draw z1{down}..z2{right}..z3{up}..z4..z5{up}..z6{right}..z7{down};
  labels(range 1 thru 7);
endchar;

begindofchar("T",14,cap#,0,-.5s#);
  x1=x2=w/2; lft x3=leftstemloc-.25s; x4=w-x3;
  bot y1=top y2-h=0; y2=y3=y4;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("U",14,cap#,0,0);
  lft x1=leftstemloc; x2=x1; x4=x5=w-x1; x3=w/2;
  bot y3+o=top y1-h=0; y5=y1;
  y2=x3-x2; y4=y2;
  draw z1--z2{down}..z3..z4{up}--z5;
  labels(range 1 thru 5);
endchar;

begindofchar("V",16,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1; 
  x2+1.5xgap=x3-1.5xgap=w/2;
  bot y2=0; top y1=h; y1=y4; y2=y3;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("W",22,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x7=w-x1; x4=w/2;
  x2+xgap=x3-xgap=.5[x1,x4];
  x5+xgap=x6-xgap=.5[x4,x7];
  top y1=h; y4+ygap=y7=y1;
  bot y2=0; y3=y5=y6=y2;
  draw z1--z2--z3--z4--z5--z6--z7;
  labels(range 1 thru 7);
endchar;

begindofchar("X",14,cap#,0,0);
  lft x1=leftstemloc-.3u; x4=w-x1; x3=x1+.3u; x4=x2+.3u;
  bot y1=top y3-h=0; y1=y4; y3=y2;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("Y",14,cap#,0,0);
  x1=x2=w/2; lft x3=leftstemloc; x4=w-x3;
  bot y1=top y3-h=0; y4=y3; bot y2=barheight;
  draw z1--z2; draw z2--z3; draw z2--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("Z",14,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x3=x1; x2=x4=w-x1;
  bot y3=top y1-h=0; y2=y1; y4=y3;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4); 
endchar;
