mode_setup;

% font_identifier:="DOF-1";
% font_coding_scheme:="AEFMNOT only";

font_size 10pt#;
% font_quad 120pt#;
% font_normal_space 120pt#;
% font_normal_stretch 120pt#;
% font_normal_shrink 120pt#;

em#:=11pt#; cap#:=10pt#; desc#:=3.5pt#; xheight#:=0.5cap#;
barheight#:=.5cap#; ebarheight#:=.55*xheight#; s#:=1/2pt#; o#:=1/5pt#;
pen_x#:=1.2pt#; pen_y#:=0.65pt#; pen_angle:=12; 
pen_lower_cor:=0.85; pen_lower_x#:=pen_lower_cor*pen_x#; pen_lower_y#:=pen_lower_cor*pen_y#;
serifize#:=0.4pt#;

define_pixels(em,cap,desc,xheight,s,serifize);
define_good_y_pixels(barheight,ebarheight);
define_corrected_pixels(o);
define_blacker_pixels(pen_x,pen_y,pen_lower_x,pen_lower_y);

pickup pencircle xscaled pen_x yscaled pen_y rotated pen_angle; dof_pen:=savepen;
pickup pencircle xscaled pen_lower_x yscaled pen_lower_y rotated pen_angle; dof_x_pen:=savepen;

beginchar("D",0.75em#,cap#,0); "The letter D";
  pickup dof_pen;
  bot lft z1=top lft z2-h*up=origin;
  z3=z1+left*serifize;
  z5=z2+left*serifize;
  rt z4=(w+o,h/2);
  draw z1..z2;
  draw z3{right}..z4..z5{left};
  % labels(1,2,3,4,5);
endchar;

beginchar("O",em#,cap#,0); "The letter O";
  pickup dof_pen;
  bot z1-o*down=top z3-(h+o)*up=(w/2,0);
  lft z4=rt z2-right*w=(0,h/2);
  draw z1{right}..z2{up}..z3{left}..z4{down}..cycle;
  % labels(1,2,3,4);
endchar;

beginchar("F",0.75em#,cap#,0); "The letter F";
  pickup dof_pen;
  lft x1=lft x2=lft x3+serifize=lft x5+serifize=0;
  bot y1=top y2-h=top y3-h=bot y5-barheight=0;
  y4=y3;
  y6=y5;
  rt x4-w=rt x6-w=lft x1;
  draw z1--z2;
  draw z3--z4;
  draw z5--z6;
endchar;

beginchar("e",0.5em#,xheight#,0); "The letter e";
  pickup dof_x_pen;
  rt z1-ebarheight/3*up=rt z5-ebarheight*up=(w,0);
  bot z2-o*down=top z4-(h+o)*up=(w/2,0);
  lft z3=(0,ebarheight);
  draw z1..z2{left}..z3{up}..z4{right}..z5{down}--z3;
  % labels(range 1 thru 5);
endchar;

beginchar("g",0.5em#,xheight#,desc#); "The letter g";
  pickup dof_x_pen;
  lft z1=rt z3-w*right=(0,h/2);
  bot z2-o*down=top z4-(h+o)*up=(w/2,0);
  draw z1..z2..z3..z4..cycle;
  rt z6=rt z5-h*up=(w,0);
  bot z7=(w/2,-d);
  lft z8=(0,-d+h/3);
  draw z5--z6{down}..z7{left}..z8;
  % labels(range 1 thru 8);
endchar;

beginchar("r",0.5em#,xheight#,0); "The letter r";
  pickup dof_x_pen;
  bot lft z1=top lft z2-h*up=origin;
  lft z3=(0,h/2);
  top z4=(2/3*w,h);
  rt z5=(w,h-ebarheight/3);
  draw z1--z2;
  draw z1{up}..z4{right}..z5;
  % labels(range 1 thru 5);
endchar;

beginchar("s",0.5em#,xheight#,0); "The letter s";
  pickup dof_x_pen;
  rt z1=lft z6 rotatedaround ((w/2,h/2),180)=(w,3/4*h);
  z3=z6 reflectedabout ((0,h/2),(w,h/2));
  z4=z1 reflectedabout ((0,h/2),(w,h/2));
  bot z5-o*down=top z2-h*up-o*up=(w/2,0);
  draw z1{up}..z2..z3..z4..z5..z6{up};
  % labels(range 1 thru 6);
endchar;

beginchar("o",0.5em#,xheight#,0); "The letter o";
  pickup dof_x_pen;
  bot z1-o*down=top z3-(h+o)*up=(w/2,0);
  lft z4-o*left=rt z2-(w+o)*right=(0,h/2);
  draw z1{right}..z2{up}..z3{left}..z4{down}..cycle;
  % labels(range 1 thru 4);
endchar;

beginchar("f",0.5em#,cap#,desc#); "The letter f";
  pickup dof_x_pen;
  lft x5=x1-w/3=x2-w/3=x3-(w/3)*2=rt x4-w=0;
  bot y1+d=y2-(h/3)*2=top y3-h=top y4-h+.2*h=0;
  x6=x3;
  y5=y6=ebarheight;
  draw z1--z2{up}..z3..z4{down};
  draw z5--z6;
  % labels(range 1 thru 4);
endchar;

beginchar("d",0.5em#,cap#,0); "The letter d";
  pickup dof_x_pen;
  bot z1-o*down=top z3-(xheight+o)*up=(w/2,0);
  lft z4-o*left=rt z2-w*right=(0,xheight/2);
  bot rt z5=top rt z6-h*up=(w,0);
  draw z1{right}..z2{up}..z3{left}..z4{down}..cycle;
  draw z5--z6;
  % labels(range 1 thru 6);
endchar;

beginchar("m",0.7em#,xheight#,0); "The letter m";
  pickup dof_x_pen;
  lft x1=lft x2=lft x3=0;
  x5=x6=w/2;
  rt x8=rt x9=w;
  x4-w/4=x7-(w/4)*3=0;
  bot y1=bot y6=bot y9=0;
  top y2=top y4-o=top y7-o=h;
  y3=y5=y8=ebarheight;
  draw z1--z2;
  draw z3{up}..z4..z5{down}--z6;
  draw z5{up}..z7..z8{down}--z9;
  % labels(range 1 thru 9);
endchar;

% font_size 120pt#; intended type size of output
% font_quad 1pt#; the unit of measure that a TEX user calls one ‘em’ when this font is selected
% font_normal_space unit#; space...
% font_normal_stretch unit#; ...stretch...
% font_normal_shrink unit#; ...and shrink define the interword spacing when text is being typeset in this font

end
