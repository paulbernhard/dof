% DOF-2 UPPERCASE

begindofchar("A",16,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1;
  x2+.35penx=x3-.35penx=w/2;
  bot y1=0; y4=y1; top y2=h; y3=y2;
  draw z1--z2--z3--z4;
  top y5=barheight; y6=y5;
  z5=whatever[z1,z2];
  z6=whatever[z3,z4];
  draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    bot y7=0; y10=y7;
    top y8=h; y9=y8;
    z7=whatever[z1,z2]; z8=whatever[z1,z2];
    z9=whatever[z3,z4]; z10=whatever[z3,z4];
    draw z7--z8--z9--z10;
    labels(range 7 thru 10);
  fi
endchar;

begindofchar("B",14,cap#,0,0);
  lft x1=leftstemloc; x2=x1; 
  x3=x6=x8=x1-overdraw; x5=x1+2xgap;
  bot y1=top y2-h=0; y8=y1; y3=y2; y5=y6=barheight;
  x4=w-x1; x7=w-x1+o;
  y4=y6+(y3-y6)/2; y7=y8+(y6-y1)/2;
  draw z1--z2;
  super_crescent(3,4,5); draw z5--z6;
  super_crescent(5,7,8);
  labels(range 1 thru 8);
  if squarish:
    pickup vsquare_pen;
    x9=x10=x1; bot y9=0; y10=h-y9;
    draw z9--z10;
    labels(9,10);
  fi
endchar;

begindofchar("C",14,cap#,0,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=w-x3+.25s;
  y1=.3[y2,y3]; y5=.3[y4,y3];
  x2=x4=w/2;
  bot y2+o=top y4-h-o=0;
  super_crescent(2,3,4);
  super_half(3,4,5);
  super_half(3,2,1);
  labels(range 1 thru 5);
  if squarish:
    pickup vsquare_pen;
    x6=x1; x7=x5;
    top y6=y1+peny/2; bot y7=y5-peny/2;
    draw z1--z6; draw z5--z7;
    labels(6,7);
  fi
endchar;

begindofchar("D",14,cap#,0,-.25s#);
  lft x1=leftstemloc; x2=x3+overdraw=x5+overdraw=x1;
  rt x4=w-leftstemloc+o+.25s;
  bot y1=0; y5=y1; 
  top y2=h; y3=y2; y4=h/2;
  draw z1--z2; 
  super_crescent(3,4,5);
  labels(range 1 thru 5);
  if squarish:
    pickup vsquare_pen;
    x6=x7=x1; bot y6=0; y7=h-y6;
    draw z6--z7;
    labels(6,7);
  fi
endchar;

begindofchar("E",14,cap#,0,-.25s#);
  lft x1=lft x2=leftstemloc; x3=x5=x7=x1-overdraw;
  x4=w-x1+.25s; x6+xgap=x8=x4;
  bot y1=top y2-h=0; y3=y2; y7=y1; y5=h/2;
  y4=y3; y6=y5; y8=y7;
  draw z1--z2;
  draw z3--z4; draw z5--z6; draw z7--z8;
  labels(range 1 thru 8);
  if squarish:
    pickup vsquare_pen;
    x9=x10=x1; bot y9=0; y10=h-y9;
    draw z9--z10;
    pickup hsquare_pen;
    rt x11=x4+penx/2; rt x12=x6+penx/2; x13=x11;
    y11=y4; y12=y6; y13=y8;
    draw z4--z11; draw z6--z12; draw z8--z13;
    labels(range 9 thru 13);
  fi
endchar;

begindofchar("F",14,cap#,0,-.25s#);
  lft x1=lft x2=leftstemloc; x3=x5=x1-overdraw;
  x4=w-x1+.25s; x6+xgap=x4;
  bot y1=top y2-h=0; y3=y2; 
  y5=h/2; y4=y3; y6=y5;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    x7=x8=x1; bot y7=0; y8=h-y7;
    draw z7--z8;
    pickup hsquare_pen;
    rt x9=x4+penx/2; rt x10=x6+penx/2;
    y9=y4; y10=y6;
    draw z4--z9; draw z6--z10;
    labels(range 7 thru 10);
  fi
endchar;

begindofchar("G",14,cap#,0,0);
  lft x3+o=leftstemloc; y3=h/2;
  x1=x5=w-x3+.25s; x2=x4=w/2;
  x6=x5; lft x7=x4;
  y1=.3[y2,y3]; y5=.3[y4,y3];
  bot y2+o=top y4-h-o=0;
  y6=y7=h/2;
  super_crescent(2,3,4);
  super_half(3,4,5);
  super_half(3,2,1);
  draw z1--z6--z7;
  labels(range 1 thru 7);
  if squarish:
    pickup vsquare_pen;
    x8=x5; bot y8=y5-peny/2;
    draw z5--z8;
    pickup hsquare_pen;
    rt x9=x6+penx/2; lft x10=x7-penx/2;
    y9=y10=y6;
    draw z9--z10;
    labels(range 8 thru 10);
  fi
endchar;

begindofchar("H",14,cap#,0,0);
  lft x1=leftstemloc; x1=x2=x5+overdraw; x3=x4=x6-overdraw=w-x1;
  bot y1=bot y3=top y2-h=0; y2=y4; bot y5=barheight; y6=y5;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    x7=x8=x1; x9=x10=x3;
    bot y7=0; y8=h-y7; y9=y7; y10=y8;
    draw z7--z8; draw z9--z10;
  fi
endchar;

begindofchar("I",7,cap#,0,0);
  x1=x2=w/2; x3=x5=x1-overdraw; x4=x6=x1+overdraw;
  bot y1=top y2-h=0; y1=y3=y4; y2=y5=y6;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    x7=x8=x1; bot y7=0; y8=h-y7;
    draw z7--z8;
    labels(7,8);
  fi
endchar;

begindofchar("J",12,cap#,0,-.5s#);
  rt x1=w-leftstemloc; x2=x5+overdraw=x6-overdraw=x1;
  lft x4=leftstemloc-.5s; x3=.5[x4,x2];
  top y1=h; y5=y6=y1; 
  bot y3=0; y2=y4=.5[x4,x2]-x4; 
  draw z1--z2; 
  super_half(2,3,4);
  draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    x7=x1; x8=x4;
    top y7=h; top y8=y4+peny/2;
    draw z1--z7; draw z4--z8;
    labels(7,8);
  fi
endchar;

begindofchar("K",14,cap#,0,-.25s#);
  if squarish: pickup vsquare_pen; fi
  lft x1=leftstemloc; x2=x3=x1; 
  rt x4=w-leftstemloc+.25s; x6=x4;
  bot y1=0; top y2=h; y1=y6; y2=y4; 
  y3=barheight; y5=.3[y3,y4];
  z5=whatever[z3,z4];
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("L",14,cap#,0,-.5s#);
  lft x1=leftstemloc; x1=x2; 
  x3+overdraw=x1; rt x4=w-leftstemloc+.5s;
  bot y1=top y2-h=0; y1=y3=y4;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
  if squarish:
    pickup vsquare_pen;
    x5=x6=x1; bot y5=0; y6=h-y5;
    draw z5--z6;
    pickup hsquare_pen;
    x7=x1; rt x8=x4+penx/2; y7=y8=y1;
    draw z7--z8;
  fi
endchar;

begindofchar("M",22,cap#,0,0);
  if squarish: pickup vsquare_pen; fi
  lft x1=lft x2=leftstemloc; x4=x5=w-x1; x3=w/2;
  bot y1=top y2-h=0; y1=y5; y2=y4; y3=ygap;
  draw z1--z2--z3--z4--z5;
  labels(range 1 thru 5);
endchar;

begindofchar("N",14,cap#,0,0);
  if squarish: pickup vsquare_pen; fi
  lft x1=lft x2=leftstemloc; x3=x4=w-x1;
  bot y1=top y2-h=0; y1=y3; y2=y4;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("O",20,cap#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  super_half(4,3,2);
  super_half(4,1,2);
  labels(range 1 thru 4);
endchar;

begindofchar("P",14,cap#,0,-.25s#);
  lft x1=leftstemloc; x2=x1; 
  x3+overdraw=x5+overdraw=x1; rt x4=w-leftstemloc+o+.25s;
  bot y1=0; top y2=h; y3=y2; y5=barheight; y4=.5[y3,y5];
  draw z1--z2; super_crescent(3,4,5);
  labels(range 1 thru 5);
  if squarish:
    pickup vsquare_pen;
    x6=x7=x1; bot y6=0; y7=h-y6;
    draw z6--z7;
    labels(6,7);
  fi
endchar;

begindofchar("Q",20,cap#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  super_half(4,3,2);
  super_half(4,1,2);
  if squarish: pickup vsquare_pen; fi
  bot rt z6=(w-leftstemloc,0); z5=.45[z6,(w/2,h/2)];
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("R",14,cap#,0,0);
  lft x1=leftstemloc; x2=x3+overdraw=x5+overdraw=x1;
  rt x4=w-leftstemloc+o+.5u; 
  x6=.3[x5,x4]; x7=x4;
  bot y1=0; y7=y1; top y2=h; y3=y2; 
  y5=barheight; y6=y5; y4=.5[y3,y5];
  draw z1--z2; 
  super_crescent(3,4,5);
  draw z6--z7;
  labels(range 1 thru 7);
  if squarish:
    pickup vsquare_pen;
    x8=x9=x1; bot y8=0; y9=h-y8;
    z10=z6; y11=y8;
    z11=whatever[z6,z7];
    draw z8--z9; draw z10--z11;
    labels(range 8 thru 11);
  fi
endchar;

begindofchar("S",14,cap#,0,0);
  lft x1=leftstemloc; x5+o=x1;
  x3=w-x1; x7=x3; x2=x4=x6=w/2;
  bot y2+o=0; top y6-o=h; y4=h/2;
  y3=.5[y4,y2]; y5=.5[y6,y4];
  y1=.3[y2,y4]; y7=.3[y6,y4];
  % draw z1{down}..z2{right}..z3{up}..z4..z5{up}..z6{right}..z7{down};
  super_half(1,2,3);
  super_half(5,6,7);
  draw z3{up}..z4..z5{up};
  labels(range 1 thru 7);
  if squarish:
    pickup vsquare_pen;
    x8=x1; x9=x7;
    top y8=y1+peny/2; bot y9=y7-peny/2;
    draw z1--z8; draw z7--z9;
    labels(8,9);
  fi
endchar;

begindofchar("T",14,cap#,0,-.5s#);
  if squarish: pickup vsquare_pen; fi
  x1=x2=w/2; bot y1=top y2-h=0; 
  draw z1--z2; 
  if squarish: pickup hsquare_pen; fi
  lft x3=leftstemloc-.25s; x4=w-x3;
  top y3=h; y4=y3;
  draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("U",14,cap#,0,0);
  lft x1=leftstemloc; x2=x1; x4=x5=w-x1; x3=w/2;
  bot y3+o=top y1-h=0; y5=y1;
  y2=x3-x2; y4=y2;
  draw z1--z2; draw z4--z5;
  super_half(2,3,4);
  labels(range 1 thru 5);
  if squarish:
    pickup vsquare_pen;
    x6=x1; x7=x5;
    top y6=h; y7=y6;
    draw z1--z6; draw z5--z7;
    labels(6,7);
  fi
endchar;

begindofchar("V",16,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1; 
  x2+.35penx=x3-.35penx=w/2;
  bot y2=0; top y1=h; y1=y4; y2=y3;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
  if squarish:
    pickup vsquare_pen;
    top y5=h; bot y6=0; y7=y6; y8=y5;
    z5=whatever[z1,z2]; z6=whatever[z1,z2];
    z7=whatever[z3,z4]; z8=whatever[z3,z4]; 
    draw z5--z6--z7--z8; 
    labels(range 5 thru 8);
  fi
endchar;

begindofchar("W",24,cap#,0,-.5s#);
  if squarish: pickup vsquare_pen; fi
  lft x1=leftstemloc-.25s; x7=w-x1; x4=w/2;
  x2+.35penx=x3-.35penx=.5[x1,x4];
  x5+.35penx=x6-.35penx=.5[x4,x7];
  top y1=h; y4+ygap=y7=y1;
  bot y2=0; y3=y5=y6=y2;
  draw z1--z2--z3--z4--z5--z6--z7;
  labels(range 1 thru 7);
endchar;

begindofchar("X",14,cap#,0,0);
  if squarish: pickup vsquare_pen; fi
  lft x1=leftstemloc-.3u; x4=w-x1; x3=x1+.3u; x4=x2+.3u;
  bot y1=top y3-h=0; y1=y4; y3=y2;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("Y",14,cap#,0,0);
  x1=x2=w/2; lft x3=leftstemloc; x4=w-x3;
  bot y1=top y3-h=0; y4=y3; bot y2=barheight;
  draw z1--z2; 
  super_arc(3,2);
  super_arc(4,2);
  labels(range 1 thru 4);
  if squarish:
    pickup vsquare_pen;
    x5=x1; x6=x3; x7=x4;
    bot y5=0; y6=y7=h-y5;
    draw z1--z5; draw z3--z6; draw z4--z7;
    labels(range 5 thru 7);
  fi
endchar;

begindofchar("Z",14,cap#,0,-.5s#);
  % "squarish" Z for future
  % if squarish: pickup hsquare_pen; fi
  % lft x1=leftstemloc-.25s; x2=w-x1; x6=x2; x5=x1;
  % top y1=h; y2=y1; bot y5=0; y6=y5;
  % draw z1--z2; draw z5--z6;
  % if squarish: pickup vsquare_pen; fi
  % rt x3=x2; lft x4=x5;
  % y3=y2-peny/2; y4=y5+peny/2;
  % draw z3--z4;
  % labels(range 1 thru 6);

  lft x1=leftstemloc-.25s; x2=w-x1; x4=x2; x3=x1;
  top y1=h; y2=y1; bot y3=0; y4=y3;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

% Ä
begindofchar(hex"C4",18,cap#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1;
  x2+.35penx=x3-.35penx=w/2;
  bot y1=0; y4=y1; top y2=h; y3=y2;
  draw z1--z2--z3--z4;
  top y5=barheight; y6=y5;
  z5=whatever[z1,z2];
  z6=whatever[z3,z4];
  draw z5--z6;
  labels(range 1 thru 6);
  if squarish:
    pickup vsquare_pen;
    bot y7=0; y10=y7;
    top y8=h; y9=y8;
    z7=whatever[z1,z2]; z8=whatever[z1,z2];
    z9=whatever[z3,z4]; z10=whatever[z3,z4];
    draw z7--z8--z9--z10;
    labels(range 7 thru 10);
  fi
  pickup round_pen;
  pickup currentpen scaled puncscale;
  rt x11=w/2-.2penx; x12=w-x11;
  bot y11=h+u; y12=y11;
  drawdot z11; drawdot z12;
  labels(11,12);
endchar;

% Ö
begindofchar(hex"D6",20,cap#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  super_half(4,3,2);
  super_half(4,1,2);
  pickup round_pen;
  pickup currentpen scaled puncscale;
  rt x5=w/2-.2penx; x6=w-x5;
  bot y5=h+u; y6=y5;
  drawdot z5; drawdot z6;
  labels(range 1 thru 6);
endchar;

% Ü
begindofchar(hex"DC",16,cap#,0,0);
  lft x1=leftstemloc; x2=x1; x4=x5=w-x1; x3=w/2;
  bot y3+o=top y1-h=0; y5=y1;
  y2=x3-x2; y4=y2;
  draw z1--z2; draw z4--z5;
  super_half(2,3,4);
  labels(range 1 thru 5);
  if squarish:
    pickup vsquare_pen;
    x6=x1; x7=x5;
    top y6=h; y7=y6;
    draw z1--z6; draw z5--z7;
    labels(6,7);
  fi
  pickup round_pen;
  pickup currentpen scaled puncscale;
  rt x8=w/2-.2penx; x9=w-x8;
  bot y8=h+u; y9=y8;
  drawdot z8; drawdot z9;
  labels(8,9);
endchar;
