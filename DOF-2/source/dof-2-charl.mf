% DOF-2 LOWERCASE CHARACTERS

begindofchar("a",13,xheight#,0,0);
  % draw different a for slanted typeface
  if slant<>0:
    rt x7=w-leftstemloc; x6=x1=x5=x7; 
    lft x3=leftstemloc-o; x2=x4=w/2;
    bot y7=top y6-h=0; y4+o=y7; y2-o=y6; y3=h/2; 
    y5=.6[y4,y3]; y1=h-y5;
    super_half(5,4,3);
    super_half(3,2,1);
    draw z6--z7;
    labels(range 1 thru 7);
  else:
    x1=x3=x7=w/2; 
    lft x4=leftstemloc; x8=x4;
    x5=x2=x6=w-x4;
    bot y5=0; y1=y5-o; 
    top y3=2/3*h+o; y2=y4=.5[y1,y3]; 
    bot y6=y3; y8=.25[y7,y3]; y7=h-y1;
    super_half(4,1,2);
    super_half(4,3,2);
    draw z5--z6;
    super_half(6,7,8);
    labels(range 1 thru 8);
  fi
endchar;

begindofchar("b",13,cap#,0,0);
  lft x1=leftstemloc; x2=x3=x7=x1; 
  x5=w-x1+o; x4=x6=w/2;
  bot y1=top y2-h=0; y6=y1-o; 
  y4=xheight+o; y5=.5[y6,y4];
  y7=.6[y6,y5]; y3=xheight-y7;
  draw z1--z2; 
  super_half(3,4,5);
  super_half(5,6,7);
  labels(range 1 thru 7);
endchar;

begindofchar("c",13,xheight#,0,0);
  lft x3+o=leftstemloc; x1=x5=w-x3-o; x2=x4=w/2;
  bot y2=0-o; y3=h/2; y4=h-y2;
  y1=.3[y2,y3]; y5=h-y1;
  super_half(3,2,1);
  super_half(3,4,5);
  labels(range 1 thru 5);
endchar;

begindofchar("d",13,cap#,0,0);
  rt x1=w-leftstemloc; x2=x3=x7=x1; 
  x5=w-x1; x4=x6=w/2;
  bot y1=0; y2=h-y1; 
  y4=y1-o; y6=xheight-y4;
  y5=.5[y4,y6]; 
  y3=.6[y4,y5]; y7=xheight-y3;
  draw z1--z2;
  super_half(3,4,5);
  super_half(5,6,7);
  labels(range 1 thru 7);
endchar;

begindofchar("e",13,xheight#,0,-.25s#);
  lft x3+o=leftstemloc; x1=x5=w-x3-o; 
  x2=x4=w/2; x6=x3-overdraw;
  bot y2=0-o; y3=ebarheight; y4=h-y2;
  y1=.3[y2,y3]; y5=y6=y3;
  super_half(1,2,3);
  super_half(3,4,5);
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("f",10,cap#,0,-.75s#);
  lft x1=leftstemloc; 
  x2=x5+overdraw=x7+overdraw=x8-overdraw=x1;
  rt x4=w-leftstemloc+.75s; x6=x4-xgap; x3=.5[x2,x4];
  bot y5=barheight; y6=y5;
  bot y1=0; y7=y8=y1; top y3-o=h;
  y2=y4=h-(1[x2,x4]-x2);
  draw z1--z2; 
  super_half(2,3,4);
  draw z5--z6; draw z7--z8;
  labels(range 1 thru 8);
endchar;

begindofchar("g",13,xheight#,desc#,0);
  rt x1=w-leftstemloc; x2=x5=x9=x1; 
  x3=x6=x8=w/2; x4=x7+o=w-x1;
  top y1=h; y2=y4=.5[y3,y6];
  bot y3=-d-o; y6=y3+d; y8=h-y6;
  y7=.5[y6,y8]; y5=.3[y6,y7]; y9=h-y5;
  draw z1--z2;
  super_half(2,3,4);
  super_half(5,6,7);
  super_half(7,8,9);
  labels(range 1 thru 9);
endchar;

begindofchar("h",13,cap#,0,0);
  lft x1=leftstemloc; x2=x3=x1;
  x5=w-x1; x6=x5; x4=.5[x3,x5];
  bot y1=0; y6=y1; 
  top y2=h; top y4=xheight+o;
  y5=.5[y6,y4]; y3=.3[y4,y5];
  draw z1--z2; 
  super_half(3,4,5);
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("i",5,cap#,0,0);
  x1=x2=w/2; x3=x5=x1-overdraw; x4=x6=x1+overdraw;
  bot y1=top y2-xheight=0; y1=y3=y4; y2=y5=y6; 
  z7=(x1,xheight+(h-xheight)/2);
  draw z1--z2; draw z3--z4; draw z5--z6;
  drawdot z7;
  labels(range 1 thru 7);
endchar;

begindofchar("j",10,cap#,desc#,-s#);
  rt x1=w-leftstemloc; x2=x7=x5+overdraw=x6-overdraw=x1;
  lft x4=0; x3=.5[x4,x2];
  top y1=xheight; y5=y6=y1; bot y3+o=-d; 
  y2=y4=-d+(1[x4,x2]-x4); y7=.5[xheight,h];
  draw z1--z2;
  super_half(2,3,4);
  draw z5--z6; drawdot z7;
  labels(range 1 thru 7);
endchar;

begindofchar("k",13,cap#,0,-.5s#);
  lft x1=leftstemloc; x2=x3=x1; 
  rt x4=w-leftstemloc+.5s; x6=x4; 
  bot y1=0; y6=y1; y2=h-y1; 
  y4=xheight; y3=ebarheight; 
  y5=.3[y3,y4]; z5=whatever[z3,z4];
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("l",5,cap#,0,0);
  x1=x2=w/2; 
  bot y1=0; y2=h-y1;
  draw z1--z2;
  labels(1,2);
endchar;

begindofchar("m",20,xheight#,0,0);
  lft x1=leftstemloc; x2=x3=x1; 
  x8=x9=w-x1; x5=x6=.5[x1,x9];
  x4=.5[x1,x6]; x7=.5[x6,x9];
  bot y1=0; y6=y9=y1;
  top y2=h; y4=y7=y2+o;
  y5=y8=h/2; y3=.6[y4,y5];
  draw z1--z2;
  super_half(3,4,5);
  draw z5--z6;
  super_half(5,7,8);
  draw z8--z9;
  labels(range 1 thru 9);
endchar;

begindofchar("n",13,xheight#,0,0);
  lft x1=leftstemloc; x2=x3=x1;
  x5=w-x1; x6=x5; x4=.5[x3,x5];
  bot y1=0; y6=y1; 
  top y2=h; y4-o=y2;
  y5=.5[y4,y6]; y3=.6[y4,y5];
  draw z1--z2; 
  super_half(3,4,5);
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("o",16,xheight#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  super_half(4,3,2);
  super_half(4,1,2);
  labels(range 1 thru 4);
endchar;

begindofchar("p",13,xheight#,desc#,-.25s#);
  lft x1=leftstemloc; x2=x3=x7=x1;
  x4=x6=w/2; x5=w-x1+.25s;
  bot y1+d=top y2-h=0; 
  bot y4+o=top y6-h-o=0; 
  y5=.5[y4,y6];
  y3=.6[y4,y5]; y7=h-y3; 
  draw z1--z2;
  super_half(7,6,5);
  super_half(3,4,5);
  labels(range 1 thru 7);
endchar;

begindofchar("q",13,xheight#,desc#,-.25s#);
  rt x1=w-leftstemloc; x2=x3=x7=x1;
  x4=x6=w/2; lft x5=leftstemloc-.25s;
  bot y1+d=bot y4+o=top y2-h=0; y6-o=y2;
  y5=.5[y4,y6];
  y3=.6[y4,y5]; y7=h-y3; 
  draw z1--z2;
  super_half(7,6,5);
  super_half(3,4,5);
  labels(range 1 thru 7);
endchar;

begindofchar("r",12,xheight#,0,-.5s#);
  lft x1=leftstemloc; x2=x3=x6+overdraw=x7-overdraw=x1;
  rt x5=w-leftstemloc+.5s; x4=.5[x3,x5];
  bot y1=0; y6=y7=y1; 
  top y2=h; y4=y2+o;
  y3=h-.6(h/2); y5=h-.3(h/2);
  draw z1--z2; draw z6--z7;
  super_half(3,4,5);
  labels(range 1 thru 7);
endchar;

begindofchar("s",13,xheight#,0,0);
  lft x1=leftstemloc; x5+o=x1;
  x3=w-x1; x7=x3; x2=x4=x6=w/2;
  bot y2+o=0; top y6-o=h; y4=h/2;
  y3=.5[y4,y2]; y5=.5[y6,y4];
  y1=.3[y2,y4]; y7=.3[y6,y4];
  super_half(1,2,3);
  super_half(5,6,7);
  draw z3{up}..z4..z5{up};
  labels(range 1 thru 7);
endchar;

begindofchar("t",10,cap#,0,-s#);
  x1=x2=x5+overdraw=x6-overdraw=w/2;
  lft x3=leftstemloc-.5s; x4=w-x3;
  bot y1=0; y5=y6=y1; top y2=h;
  top y3=xheight; y4=y3;
  draw z1--z2; draw z3--z4; draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("u",13,xheight#,0,0);
  lft x6=leftstemloc; x5=x6; 
  x1=x2=x3=w-x6; x4=.5[x5,x3];
  bot y1=0; y4=y1-o;
  top y2=h; y6=y2;
  y5=.5[y4,y6]; y3=.6[y4,y5];
  draw z1--z2; 
  super_half(3,4,5);
  draw z5--z6;
  labels(range 1 thru 6);
endchar;

begindofchar("v",13,xheight#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1;
  x2+.35penx=x3-.35penx=w/2;
  top y1=h; y4=y1;
  bot y2=0; y3=y2;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("w",22,xheight#,0,-.5s#);
  lft x1=leftstemloc-.25s; x7=w-x1; x4=w/2;
  x2+.35penx=x3-.35penx=.5[x1,x4];
  x5+.35penx=x6-.35penx=.5[x4,x7];
  top y1=h; y4=y7=y1;
  bot y2=0; y3=y5=y6=y2;
  draw z1--z2--z3--z4--z5--z6--z7;
  labels(range 1 thru 7);
endchar;

begindofchar("x",13,xheight#,0,-.5s#);
  lft x1=leftstemloc-.25s; x4=w-x1; x3=x1+.3u; x4=x2+.3u;
  bot y1=top y3-h=0; y1=y4; y3=y2;
  draw z1--z2; draw z3--z4;
  labels(range 1 thru 4);
endchar;

begindofchar("y",13,xheight#,desc#,-.25s#);
  % lft x1=leftstemloc-.25s; x3=w-x1; x2=w/2;
  % top y1=h; y3=y1; bot y2=0; bot y4=-d;
  % z4=whatever[z3,z2];
  % draw z1--z2;;
  % draw z3--z4;
  % labels(range 1 thru 4);

  lft x1=leftstemloc; x3=w-x1+.25s; x4=.5[x1,x3];
  top y1=h; y3=y1; y2=h-y1; y4=h-y1-d;
  z2=whatever[z4,z3];
  draw z3--z4;
  super_arc(1,2);
  labels(range 1 thru 4);
endchar;

begindofchar("z",13,xheight#,0,-.5s#);
  lft x1=leftstemloc-.25s; x3=x1; x2=x4=w-x1;
  bot y3=top y1-h=0; y2=y1; y4=y3;
  draw z1--z2--z3--z4;
  labels(range 1 thru 4); 
endchar;

% ä
begindofchar(hex"E4",13,xheight#,0,0);
  % draw different ä for slanted typeface
  if slant<>0:
    rt x7=w-leftstemloc; x6=x1=x5=x7; 
    lft x3=leftstemloc-o; x2=x4=w/2;
    bot y7=top y6-h=0; y4+o=y7; y2-o=y6; y3=h/2; 
    y5=.6[y4,y3]; y1=h-y5;
    super_half(5,4,3);
    super_half(3,2,1);
    draw z6--z7;
    labels(range 1 thru 7);
  else:
    x1=x3=x7=w/2; 
    lft x4=leftstemloc; x8=x4;
    x5=x2=x6=w-x4;
    bot y5=0; y1=y5-o; 
    top y3=2/3*h+o; y2=y4=.5[y1,y3];
    bot y6=y3; y8=.25[y7,y3]; y7=h-y1;
    super_half(4,1,2);
    super_half(4,3,2);
    draw z5--z6;
    super_half(6,7,8);
    labels(range 1 thru 8);
  fi
  % add dots
  pickup currentpen scaled puncscale;
  rt x9=w/2-.2penx; x10=w-x9;
  bot y9=h+u; y10=y9;
  drawdot z9; drawdot z10;
  labels(9,10);
endchar;

% ö
begindofchar(hex"F6",16,xheight#,0,-.5s#);
  x1=x3=w/2; lft x4+o=leftstemloc-.25s; x2=w-x4;
  bot y1+o=top y3-h-o=0; y4=y2=h/2;
  super_half(4,3,2);
  super_half(4,1,2);
  pickup currentpen scaled puncscale;
  rt x5=w/2-.2penx; x6=w-x5;
  bot y5=h+u; y6=y5;
  drawdot z5; drawdot z6;
  labels(range 1 thru 6);
endchar;

% ü
begindofchar(hex"FC",13,xheight#,0,0);
  lft x6=leftstemloc; x5=x6; 
  x1=x2=x3=w-x6; x4=.5[x5,x3];
  bot y1=0; y4=y1-o;
  top y2=h; y6=y2;
  y5=.5[y4,y6]; y3=.6[y4,y5];
  draw z1--z2; 
  super_half(3,4,5);
  draw z5--z6;
  pickup currentpen scaled puncscale;
  rt x7=w/2-.2penx; x8=w-x7;
  bot y7=h+u; y8=y7;
  drawdot z7; drawdot z8;
  labels(range 1 thru 8);
endchar;
